apply plugin: 'chan-extension'

chan {
	versionName '1.43-fix8'
	apiVersion 1
	hosts '2ch.hk', '2ch.pm', '2ch.cm', '2ch.re', '2ch.tf', '2ch.wf', '2ch.yt', '2-ch.so'
}

dependencies {
	implementation 'org.jsoup:jsoup:1.15.2'
}

android {
	signingConfigs {
		release {

			def keystoreProperties = new Properties()
			try {
				file('keystore.properties').newInputStream().withCloseable { keystoreProperties.load(it) }
			} catch (IOException ignored) {
				keystoreProperties = null
			}

			if (keystoreProperties == null) {
				//git workflow build
				System.out.println("keystoreProperties is null")
				def key_file = System.getenv("KEYSTORE_FILENAME")
			        storeFile file(key_file != null ? key_file : "keystore/dashchan")
				storePassword System.getenv("KEYSTORE_PASSWORD")
				keyAlias System.getenv("RELEASE_SIGN_KEY_ALIAS")
				keyPassword System.getenv("RELEASE_SIGN_KEY_PASSWORD")

			} else {
				//local build
				storeFile keystoreProperties['store.file']
				storePassword keystoreProperties['store.password']
				keyAlias keystoreProperties['key.alias']
				keyPassword keystoreProperties['key.password']
			}

		}
	}
	buildTypes {
		release {
			signingConfig signingConfigs.release
		}
	}
}
