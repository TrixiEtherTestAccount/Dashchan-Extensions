apply plugin: 'chan-extension'

chan {
	versionName '1.27-experimental-1.0'
	apiVersion 1
	hosts '4chan.org', 'www.4chan.org', 'boards.4chan.org', 'sys.4chan.org',
			'4channel.org', 'www.4channel.org', 'boards.4channel.org', 'sys.4channel.org',
			'i.4cdn.org', 'is.4chan.org', 'is2.4chan.org'
}

dependencies {
	implementation 'chan.library:template-parser:0'
}

android {
	signingConfigs {
		release {

			def keystoreProperties = new Properties()
			try {
				file('keystore.properties').newInputStream().withCloseable { keystoreProperties.load(it) }
			} catch (IOException ignored) {
				keystoreProperties = null
			}

			if (keystoreProperties == null) {
				//git workflow build
				System.out.println("keystoreProperties is null")
				def key_file = System.getenv("KEYSTORE_FILENAME")
				storeFile file(key_file != null ? key_file : "keystore/dashchan")
				storePassword System.getenv("KEYSTORE_PASSWORD")
				keyAlias System.getenv("RELEASE_SIGN_KEY_ALIAS")
				keyPassword System.getenv("RELEASE_SIGN_KEY_PASSWORD")

			} else {
				//local build
				storeFile keystoreProperties['store.file']
				storePassword keystoreProperties['store.password']
				keyAlias keystoreProperties['key.alias']
				keyPassword keystoreProperties['key.password']
			}

		}
	}
	buildTypes {
		release {
			signingConfig signingConfigs.release
		}
	}
}